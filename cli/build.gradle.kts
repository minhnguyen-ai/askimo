import java.time.Instant
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter
import java.util.Properties

plugins {
    application
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.graalvm.native)
}

group = rootProject.group
version = rootProject.version

// Function to load environment variables from .env file
fun loadEnvFile(): Map<String, String> {
    val envFile = File(rootProject.projectDir, ".env")
    val envVars = mutableMapOf<String, String>()

    if (envFile.exists()) {
        val props = Properties()
        envFile.inputStream().use { props.load(it) }
        props.forEach { (key, value) ->
            envVars[key.toString()] = value.toString()
        }
        println("📁 Loaded ${envVars.size} environment variables from .env file")
    } else {
        println("ℹ️  No .env file found, skipping environment variable loading")
    }

    return envVars
}

dependencies {
    compileOnly(libs.graalvm.nativeimage.svm)
    implementation(libs.bundles.jline)
    implementation(libs.bundles.commonmark)
    implementation(kotlin("stdlib"))
    implementation(project(":shared"))
    testImplementation(platform(libs.junit.bom))
    testImplementation(libs.junit.jupiter)
    testImplementation(libs.mockito.kotlin)
    testImplementation(libs.kotlin.test)
    testImplementation(libs.bundles.testcontainers)
    testImplementation(testFixtures(project(":shared")))
}

tasks.test {
    useJUnitPlatform {
        excludeTags("native")
    }

    // Load environment variables from .env file if it exists
    val envVars = loadEnvFile()
    envVars.forEach { (key, value) ->
        environment(key, value)
    }

    jvmArgs =
        listOf(
            "-XX:+EnableDynamicAgentLoading",
            "--add-modules",
            "jdk.incubator.vector",
        )

    if (project.hasProperty("agent")) {
        finalizedBy("metadataCopy")
    }
}

kotlin {
    jvmToolchain((property("jvmVersion") as String).toInt())
    compilerOptions {
        javaParameters.set(true)
    }
}

application {
    mainClass.set("io.askimo.cli.ChatCliKt")
}

tasks.jar {
    manifest {
        attributes["Main-Class"] = application.mainClass.get()
    }
}

sourceSets {
    main {
        kotlin.srcDirs("src/main/kotlin")
        resources.srcDirs("src/main/resources")
    }
    test {
        kotlin.srcDirs("src/test/kotlin")
        resources.srcDirs("src/test/resources")
    }
}

tasks.processTestResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

val author = property("author") as String
val licenseId = property("licenseId") as String
val homepage = property("homepage") as String

val aboutDir = layout.buildDirectory.dir("generated-resources/about")
val aboutFile = aboutDir.map { it.file("about.properties") }

val generateAbout =
    tasks.register("generateAbout") {
        outputs.file(aboutFile)

        doLast {
            val buildDate =
                DateTimeFormatter.ISO_LOCAL_DATE
                    .withZone(ZoneOffset.UTC)
                    .format(Instant.now())

            val text =
                """
                name=Askimo
                version=${project.version}
                author=$author
                buildDate=$buildDate
                license=$licenseId
                homepage=$homepage
                buildJdk=${System.getProperty("java.version") ?: "unknown"}
                """.trimIndent()

            val f = aboutFile.get().asFile
            f.parentFile.mkdirs()
            f.writeText(text)
        }
    }

tasks.named<ProcessResources>("processResources") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn(generateAbout)
    from(aboutDir)
}

graalvmNative {
    agent {
        enabled.set(project.hasProperty("agent"))
        defaultMode.set("standard")

        callerFilterFiles.from("src/main/resources/graal-caller-filter.json")
        accessFilterFiles.from("src/main/resources/graal-access-filter.json")
        builtinCallerFilter.set(true)
        builtinHeuristicFilter.set(true)
        enableExperimentalPredefinedClasses.set(false)
        enableExperimentalUnsafeAllocationTracing.set(false)
        trackReflectionMetadata.set(true)

        modes {
            standard {}
        }

        metadataCopy {
            inputTaskNames.add("test")
            outputDirectories.add("src/main/resources/META-INF/native-image/io.askimo/cli")
            mergeWithExisting.set(true)
        }

        tasksToInstrumentPredicate.set { t -> t.name == "test" }
    }

    metadataRepository {
        enabled.set(true)
    }

    binaries {
        named("main") {
            imageName.set("askimo")
            javaLauncher.set(
                javaToolchains.launcherFor { languageVersion.set(JavaLanguageVersion.of((property("jvmVersion") as String).toInt())) },
            )

            val graalvmMemory = project.findProperty("graalvm.native-image.memory") as? String ?: "8g"

            buildArgs.addAll(
                listOf(
                    "-J-Xmx$graalvmMemory",
                    "--enable-url-protocols=https",
                    "--enable-native-access=ALL-UNNAMED",
                    "--report-unsupported-elements-at-runtime",
                    "--features=io.askimo.cli.graal.AskimoFeature",
                    "-Dorg.apache.lucene.store.MMapDirectory.enableMemorySegments=false",
                    "--initialize-at-build-time=kotlin.DeprecationLevel,kotlin.jvm.internal.Intrinsics,kotlin.enums.EnumEntries, com.github.benmanes.caffeine",
                    "--initialize-at-run-time=kotlinx.coroutines,kotlin.coroutines,io.askimo.core.project.ProjectFileWatcher",
                    "-H:IncludeResources=logback.xml|logback-.*\\.xml",
                    "--allow-incomplete-classpath",
                    "-H:+ReportExceptionStackTraces",
                ),
            )
            resources.autodetect()
            configurationFileDirectories.from(file("src/main/resources/META-INF/native-image/io.askimo/cli"))
        }
    }
}

// Remove macOS quarantine before compilation to prevent GraalVM component blocking
fun runXattr(vararg args: String) {
    try {
        ProcessBuilder("xattr", *args)
            .inheritIO()
            .start()
            .waitFor()
    } catch (_: Exception) {
        // Ignore errors — xattr may not exist or attribute may not be set
    }
}

tasks.named("nativeCompile") {
    doFirst {
        if (System.getProperty("os.name").contains("Mac", ignoreCase = true)) {
            val nativeDir = file("build/native")
            if (nativeDir.exists()) {
                runXattr("-dr", "com.apple.quarantine", nativeDir.absolutePath)
                println("🔓 Pre-cleared quarantine from build/native directory")
            }
        }
    }

    doLast {
        if (System.getProperty("os.name").contains("Mac", ignoreCase = true)) {
            val nativeDir = file("build/native")
            val binary = file("build/native/nativeCompile/askimo")

            runXattr("-dr", "com.apple.quarantine", nativeDir.absolutePath)
            println("✅ Removed quarantine from all native build artifacts")

            if (binary.exists()) {
                runXattr("-d", "com.apple.quarantine", binary.absolutePath)
                println("✅ Removed quarantine from binary: ${binary.name}")
            }
        }
    }
}
